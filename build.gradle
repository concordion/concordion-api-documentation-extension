plugins {
    id 'org.ajoberstar.git-publish' version '0.3.0'

    // deploy to maven central
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: "groovy"
apply plugin: "jacoco"

description = 'Executable API documentation in concordion'
group = 'org.concordion'
version = '0.0.5'

// bintray deployment
// configured in gradle.properties

if (hasProperty('bintray_Username')) {
    apply from: 'bintray.gradle'
}

wrapper {
    gradleVersion = "6.5"
}

repositories {
    mavenCentral()
}

project.ext {
    concordionVersion = "3.1.2"
    groovyVersion = "2.4.12"
    junitVersion = "4.11"
}

dependencies {
    compile "org.codehaus.groovy:groovy:$groovyVersion"
    compile "org.concordion:concordion:$concordionVersion"
    testCompile "junit:junit:$junitVersion"
}

test {
    systemProperty "concordion.output.dir", "build/concordion"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

// Publish github pages

// Publishes the Concordion output to Github Pages
// https://github.com/ajoberstar/gradle-git-publish
// gradle -b publish.gradle publishDocs


gitPublish {
    // where to publish to (repo must exist)
    repoUri = 'https://github.com/concordion/concordion-api-documentation-extension.git'
    // branch will be created if it doesn't exist
    branch = 'gh-pages'

    // generally, you don't need to touch this
    // repoDir = file("$buildDir/gitPublish")

    // what to publish, this is a standard CopySpec
    contents {
        from 'src/pages'
        from('build/concordion/') {
            into 'spec'
        }
    }

    // what to keep in the existing branch (include=keep)
    preserve {
        include '1.0.0/**'
        exclude '1.0.0/temp.txt'
    }

    // message used when committing changes
    commitMessage = 'Publishing github pages' // defaults to 'Generated by gradle-git-publish'
}

task publishDocs(dependsOn: ['test', 'gitPublishPush'])
